function(target_link_xoos_library target scope lib)
    set(libtarget xoos_${lib})
    if (NOT TARGET ${libtarget})
        set(subdir ${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../${lib})
        if (NOT EXISTS ${subdir})
            message(FATAL_ERROR "Target ${libtarget} subdirectory ${subdir} does not exit")
        endif ()
        add_subdirectory(${subdir} ${libtarget})
        if (NOT TARGET ${libtarget})
            message(FATAL_ERROR "Target ${libtarget} was not defined in subdirectory ${subdir}")
        endif ()
    endif ()
    target_link_libraries(${target} ${scope} ${libtarget})
endfunction()

function(target_link_xoos_libraries target)
    cmake_parse_arguments(PARSE_ARGV 1 ARG "" "" "PRIVATE;PUBLIC;INTERFACE")
    if (DEFINED ARG_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "Unrecognized arguments: ${ARG_UNPARSED_ARGUMENTS}")
    endif ()
    foreach (scope PRIVATE PUBLIC INTERFACE)
        foreach (lib ${ARG_${scope}})
            target_link_xoos_library(${target} ${scope} ${lib})
        endforeach ()
    endforeach ()
endfunction()
